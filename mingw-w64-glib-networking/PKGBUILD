# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glib-networking
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.44.0
pkgrel=2
pkgdesc="Network-related GIO modules for glib (mingw-w64)"
arch=(any)
url="http://ftp.gnome.org/pub/GNOME/sources/libsoup"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
             "intltool")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-libgcrypt"
         "${MINGW_PACKAGE_PREFIX}-p11-kit"
         "${MINGW_PACKAGE_PREFIX}-xz")
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        0001-dont-overwrite-prefix-remain-relative.mingw.patch
        0001-hack-path.patch
        0002-relative-system-ca-file.mingw.patch)
md5sums=('6989b20cf3b26dd5ae272e04a9acb0b3'
         '6c55845f6c66fc8401915d1561b6872d'
         '5b72f575aee2cd8d7b30f580ebca4971'
         'b96895469c4dc36e172056763aa0d014')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  #patch -p1 -i ${srcdir}/0001-dont-overwrite-prefix-remain-relative.mingw.patch
  patch -p1 -i ${srcdir}/0001-hack-path.patch
  patch -p1 -i ${srcdir}/0002-relative-system-ca-file.mingw.patch
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd build-${MINGW_CHOST}

  ../${_realname}-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --without-libproxy \
    --without-gnome-proxy \
    --with-ca-certificates=../ssl/certs/ca-bundle.crt \
    --with-pkcs11

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gio/modules/*.a
}
