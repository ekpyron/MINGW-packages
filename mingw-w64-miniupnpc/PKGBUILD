# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=miniupnpc
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.9.20150206
pkgrel=1
pkgdesc="A small UPnP client library/tool to access Internet Gateway Devices (mingw-w64)"
arch=('any')
url="http://miniupnp.free.fr"
license=('BSD')
options=('staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=("http://miniupnp.free.fr/files/${_realname}-$pkgver.tar.gz"
        001-link-libraries.patch)
sha256sums=('230d3ad5a3047daa77f99602503d798f720428207217ebfbfe2a4f7762b029e7'
            '3ac8b46b15ab48415cbbfa759d968873b315a96120d4dcbe062fbc39a909726d')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-link-libraries.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DUPNPC_BUILD_SHARED=ON \
    -DUPNPC_BUILD_STATIC=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd build-${MINGW_CHOST}
  make install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/man3/miniupnpc.3 \
    "${pkgdir}${MINGW_PREFIX}/share/man/man3/miniupnpc.3"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
