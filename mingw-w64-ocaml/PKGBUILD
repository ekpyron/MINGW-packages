# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.02.1
pkgrel=3
pkgdesc="An industrial strength programming language supporting functional, imperative and object-oriented styles (mingw-w64)"
arch=('any')
url='http://ocaml.org/'
license=('Q Public Licence 1.0' 'GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-flexdll")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "diffutils")
source=("http://caml.inria.fr/pub/distrib/ocaml-4.02/ocaml-4.02.1.tar.gz"
        "0002-Adapt-to-msys2-directory-layout.patch"
        "0003-Install-manuals-in-msys2.patch"
        "0004-camlheader-has-the-extension-.exe-in-msys2.patch"
        "0005-Install-libraries-to-lib-ocaml-instead-of-lib.patch"
        "0006-Configure-the-relative-paths-for-ocaml-libraries-on-.patch"
        "0007-Convert-to-windows-path-before-checking-stats.patch"
        "0008-Use-the-standard-paths-from-Config-in-ocamlbuild.patch")
sha1sums=('6af8c67f2badece81d8e1d1ce70568a16e42313e'
          '75b14de1f4ea982ddbb86f49576aa0be10f9286a'
          'a780a6c94aaedcd0d19a7fbc26e09776b581a8af'
          'e1f6b201c25b61d39404406c82684f6c8d70d23a'
          '17010515bd9715ed36300165643d8f3ad01288cf'
          '78b69527ce8a78fbb31f7b9f3e0e04bf3ffcc165'
          'cff96df7caef78097ac73a382e4ef50418a7583b'
          '8b087c4189dc2837f3ca4aa88a5b167bbaa8e882')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  mv config/Makefile.mingw{,32}
  cp config/m-nt.h config/m.h
  cp config/s-nt.h config/s.h

  patch -p1 -i ${srcdir}/0002-Adapt-to-msys2-directory-layout.patch
  patch -p1 -i ${srcdir}/0003-Install-manuals-in-msys2.patch
  patch -p1 -i ${srcdir}/0004-camlheader-has-the-extension-.exe-in-msys2.patch
  patch -p1 -i ${srcdir}/0005-Install-libraries-to-lib-ocaml-instead-of-lib.patch
  patch -p1 -i ${srcdir}/0006-Configure-the-relative-paths-for-ocaml-libraries-on-.patch
  patch -p1 -i ${srcdir}/0007-Convert-to-windows-path-before-checking-stats.patch
  patch -p1 -i ${srcdir}/0008-Use-the-standard-paths-from-Config-in-ocamlbuild.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}

  unset FLEXDIR
  unset OCAMLDIR
  cd ${srcdir}/build-${MINGW_CHOST}
  cp config/Makefile.${MINGW_PREFIX#/} config/Makefile

  make -f Makefile.nt world TOOLPREF= -j1
  make -f Makefile.nt bootstrap TOOLPREF= -j1
  make -f Makefile.nt opt TOOLPREF= -j1
  make -f Makefile.nt opt.opt TOOLPREF= -j1
}

check() {
  # The testsuite refuses to compile due to lack of headers such as sys/resource.h
  plain "skip"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -f Makefile.nt install TOOLPREF= PREFIX="${pkgdir}${MINGW_PREFIX}" -j1
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
